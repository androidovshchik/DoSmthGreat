apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "rf.androidovshchik.dosmthgreat"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 2
        versionName "1.1"
        buildConfigField "int", "DB_VERSION", "1"
        resValue "string", "app_name", "DoSmthGreat"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        debug {
            java.srcDirs = ['src/main/java', 'src/debug/java']
        }
        release {
            java.srcDirs = ['src/main/java', 'src/release/java']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        checkReleaseBuilds false
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "_DoSmthGreat-${variant.buildType.name}-${variant.versionName}.apk"
        }
        //noinspection GrDeprecatedAPIUsage
        if (!variant.getBuildType().debuggable) {
            variant.assemble.doLast {
                copy {
                    from variant.mappingFile
                    into "${rootDir}/app/release"
                    rename { String fileName ->
                        "DoSmthGreat-mapping.txt"
                    }
                }
            }
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // support
    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation "com.android.support:recyclerview-v7:$support_library_version"
    implementation "com.android.support:support-v4:$support_library_version"
    implementation "com.android.support:design:$support_library_version"

    implementation 'com.google.android.gms:play-services-ads:15.0.1'

    // rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.12'

    // butterknife
    implementation "com.jakewharton:butterknife:$butterknife_version"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife_version"

    // sqlbrite
    implementation 'com.squareup.sqlbrite3:sqlbrite:3.2.0'
    implementation "android.arch.persistence:db:$arch_db_version"
    implementation "android.arch.persistence:db-framework:$arch_db_version"

    // stetho
    debugImplementation "com.facebook.stetho:stetho:$stetho_version"
    debugImplementation "com.facebook.stetho:stetho-urlconnection:$stetho_version"

    implementation 'com.karumi:dexter:4.2.0'
    implementation 'com.snatik:storage:2.1.0'
    implementation 'com.google.code.gson:gson:2.8.4'
    implementation 'com.google.android.exoplayer:exoplayer:r2.5.4'
    implementation 'com.jakewharton.timber:timber:4.7.0'
    implementation 'ch.acra:acra:4.9.2'
    implementation 'com.daasuu:CountAnimationTextView:0.1.1'
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion support_library_version
            }
        }
    }
}